group 'net.benwoodworth'
version '2.x'

def env = System.getenv()
if (env.containsKey('TRAVIS')) {
    if (env.containsKey('TRAVIS_TAG')) {
        version = env['TRAVIS_TAG']
    } else {
        def hash = env['TRAVIS_COMMIT'].substring(0, 7)
        version += "-$hash"
    }
}

def plugin_name = 'FastCraft'
def plugin_author = 'Kepler_'
def plugin_description = 'Redefines crafting in Minecraft'
def plugin_version = version
def plugin_website = 'https://github.com/BenWoodworth/FastCraft'

apply plugin: 'java'

buildscript {
    ext {
        // version_bukkit = '1.10.2-R0.1-SNAPSHOT'
        version_bukkit = '1.12.2-R0.1-SNAPSHOT'
    }

    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}

dependencies {
    compile "org.bukkit:bukkit:$version_bukkit"
    compile files('lib/RecipesAPI.jar')
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand(
                plugin_name: plugin_name,
                plugin_author: plugin_author,
                plugin_description: plugin_description,
                plugin_version: plugin_version,
                plugin_website: plugin_website
        )

        include 'plugin.yml'
    }
}

jar {
    baseName plugin_name
}
